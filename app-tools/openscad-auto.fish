#!/usr/bin/env -S fish --no-config

if [ (count $argv) -lt 1 ]
  echo "Usage: openscad-auto file.scad" >&2
  exit 1
end

set SOURCE_FILE $argv[1]
set OUTPUT_FILE $SOURCE_FILE.stl

set LOW_FI_FALSE "LOW_FI_DEV = false;"
if cat $SOURCE_FILE | grep "^"$LOW_FI_FALSE > /dev/null
  echo "✅ "$LOW_FI_FALSE
else
  echo "Could not verify that the file is set to hi-fi. Please make sure the following line is present:"
  echo (set_color --bold)$LOW_FI_FALSE(set_color normal)
  exit 1
end

if test -e $OUTPUT_FILE
  set -l OUTPUT_FILE_BACKUP $OUTPUT_FILE.bak.(date "+%Y-%m-%d_%H-%M-%S").stl
  echo "➡️ Output file already exists. Moving to: "(set_color --bold)"$OUTPUT_FILE_BACKUP"(set_color normal)
  mv $OUTPUT_FILE $OUTPUT_FILE_BACKUP
end

set SOURCE_HASH_SHA256 (openssl dgst -sha256 $SOURCE_FILE | awk '{printf $NF}')

echo "✍️ Writing new file to: "(set_color --bold)"$SOURCE_FILE.stl"(set_color normal)
# Set `$FONTCONFIG_PATH` to avoid https://github.com/openscad/openscad/issues/2888
set OPENSCAD_OUTPUT (env FONTCONFIG_PATH=$(brew --prefix)/etc/fonts/ openscad -o $SOURCE_FILE.stl $SOURCE_FILE 2>&1 | tee /dev/stderr)

set TOTAL_RENDERING_TIME (echo $OPENSCAD_OUTPUT | grep -o "Total rendering time: \([^ ]\+\)" | awk '{print $NF}')
echo "/* Generated by `openscad-auto` on "(date "+%Y-%m-%d")" in "$TOTAL_RENDERING_TIME" from: `"(basename $SOURCE_FILE)"` (sha256: $SOURCE_HASH_SHA256) */" >> $OUTPUT_FILE

terminal-notifier -title "openscad-auto" -message "Done converting: $SOURCE_FILE
(Total rendering time: $TOTAL_RENDERING_TIME)" -execute "open -R "(string escape $OUTPUT_FILE)"`"
